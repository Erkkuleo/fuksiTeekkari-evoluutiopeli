title Fuksi, Teekkari - evoluutioeli
author Aleksi, Juha, Erkka
realtime_interval 0.5
again_interval 0.5

========
OBJECTS
========
background
lightgreen green
11000
01100
00110
00011
10001

bottomBarBattle
darkgray gray
00000
10101
01010
10101
01010


fuksi1
blue lightgreen red
.000.
...00
.....
..112
.1122

fuksi2
blue red yellow
.....
00000
.0...
11222
11222

fuksi3
blue yellow
.....
00...
.....
0....
10...

fuksi4
lightgreen red blue
.....
....0
...01
...01
...22

fuksi5
lightgreen red blue
00111
01111
11111
11111
22111

fuksi6
red yellow
00111
00011
00011
00011
00011

fuksi7
yellow blue
001..
0001.
0001.
00001
00001

fuksi8
blue
00000
00000
..000
.....
.....

fuksi9
blue darkbrown lightbrown brown black white
00000
00000
00011
12233
12545

fuksi10
blue yellow lightbrown darkbrown brown black white
00011
02222
22223
22224
24265

fuksi11
yellow blue lightbrown darkbrown brown white
00001
2223.
3323.
4223.
52233

fuksi13
darkbrown lightbrown
01111
01111
01111
01111
01111

fuksi14
darkbrown lightbrown brown
12111
21211
01011
11111
11111

fuksi15
lightbrown darkbrown
00011
00011
0001.
0001.
001..

fuksi17
lightbrown darkbrown
.1000
.1000
..110
....1
...10

fuksi18
lightbrown brown darkbrown
11100
00000
00000
22222
00000

fuksi19
lightbrown darkbrown
01...
01...
1....
.....
1....

teekkari1
black white
.....
.....
.....
..000
.0111

teekkari2
black white
.....
.....
.00..
00011
11111

teekkari3
white
.....
.....
.....
0....
000..

teekkari4
black white
....1
....0
...01
...00
...00

teekkari5
black white
01111
11111
11111
00000
00000

teekkari6
black white yellow
11111
11111
11111
02200
02200

teekkari7
black white
11111
11111
11111
00000
00000

teekkari8
black darkbrown
...0.
..0..
..0..
..0..
..0.1

teekkari9
lightbrown brown darkbrown darkgray black white
33333
33333
20223
20111
20545

teekkari10
lightbrown brown darkgray black white
22222
22222
22222
00011
01043

teekkari11
lightbrown brown darkbrown darkgray white
3333.
3333.
2202.
1002.
40022

teekkari12
black darkbrown
..0.1
..0.1
.000.
00000
00000

teekkari16
black
00000
00000
0.000
0.0.0
....0

vanhus5
black white lightbrown gray
10022
11323
11331
00000
00000

vanhus6
black white lightbrown yellow gray
44222
11442
14411
03300
03300

vanhus12
black darkbrown gray
..0.1
..0.1
.000.
00000
02002

vanhus16
black gray lightgray
12101
22212
2.222
2.2.2
....2



rock
white
.000.
00000
00000
00000
.000.

paper
white
.....
.....
.....
00000
.....

scissors
white
...0.
...0.
...0.
00000
...00

choosebox
black
00.00
0...0
.....
0...0
00.00

player
transparent
limit
transparent

(Taistelumoodin game stateja ja timereita)
chosen
transparent
enemyChosen
transparent
enemyChoosing
transparent
gameState
transparent
winState
transparent
loseState
transparent
drawState
transparent
gameEndTimer
transparent
gameEndTimer0
transparent
gameEndTimer1
transparent
gameEndTimer2
transparent
gameHasEnded
transparent
moveToNextLevel
transparent
wait1
transparent
winMessage1
transparent
winMessage2
transparent
loseMessage1
transparent
loseMessage2
transparent
drawMessage1
transparent
drawMessage2
transparent

=======
LEGEND
=======
. = background
p = player

; = fuksi1 
: = fuksi2 
- = fuksi3
_ = fuksi4
! = fuksi5
" = fuksi6
@ = fuksi7
£ = fuksi8
¤ = fuksi9
$ = fuksi10
% = fuksi11
€ = fuksi13
& = fuksi14
/ = fuksi15
{ = fuksi17
¨ = fuksi18
~ = fuksi19

ƒ = teekkari1
„ = teekkari2
… = teekkari3
† = teekkari4
‡ = teekkari5
ˆ = teekkari6
‰ = teekkari7
Š = teekkari8
‹ = teekkari9
Œ = teekkari10
Ž = teekkari11
‘ = teekkari12
’ = teekkari16

“ = vanhus5
” = vanhus6
• = vanhus12
– = vanhus16

' = rock and bottomBarBattle
* = paper and bottomBarBattle
§ = scissors and bottomBarBattle

B = bottomBarBattle

A = choosebox and rock and bottomBarBattle
½ = limit and bottomBarBattle

g = gameState
n = gameEndTimer
c = chosen
e = enemyChosen
endState = winState or loseState or drawState

winMessage = winMessage1 or winMessage2
loseMessage = loseMessage1 or loseMessage2
drawMessage = drawMessage1 or drawMessage2

Options = rock or paper or scissors

( Fuksi sprite battle modessa
.;:-.
_!"@.
£¤$%.
.€&/.
.{¨~.
)

( Teekkari sprite battle modessa
.ƒ„…
†‡ˆ‰
Š‹ŒŽ
‘€&/
’{¨~
)

( Vanhus sprite battle modessa
.ƒ„…
†“”‰
Š‹ŒŽ
•€&/
–{¨~
)

=======
SOUNDS
=======
choosebox destroy 73373703
winState create 850
drawState create 20182901
loseState create 102

================
COLLISIONLAYERS
================
background 
bottomBarBattle
player
fuksi1 fuksi2 fuksi3 fuksi4 fuksi5 fuksi6 fuksi7 fuksi8 fuksi9 fuksi10 fuksi11 fuksi13 fuksi14 fuksi15 fuksi17 fuksi18 fuksi19 teekkari1 teekkari2 teekkari3 teekkari4 teekkari5 teekkari6 teekkari7 teekkari8 teekkari9 teekkari10 teekkari11 teekkari12 teekkari16 vanhus5 vanhus6 vanhus12 vanhus16
gameState gameEndTimer gameEndTimer0 gameEndTimer1 gameendtimer2 gameHasEnded winState loseState drawState winMessage1 winMessage2 loseMessage1 loseMessage2 drawMessage1 drawMessage2 moveToNextLevel
rock paper scissors chosen enemyChosen enemyChoosing wait1
choosebox limit

======
RULES
======

(Valitsimen liikuttaminen taistelumoodissa)
horizontal [> Player ] [ choosebox ] -> [> Player ][> choosebox]

(Valitaan vaihtoehto, poistetaan kaikki vaihtoehdot ja siirretään oma valinta "pelilaudalle")
[ Action player] [rock choosebox ] [paper ][scissors] [ chosen | enemyChosen ] -> [player ][ ][][] [ rock | enemyChoosing]
[ Action player] [paper choosebox ] [rock][scissors] [ chosen | enemyChosen] -> [player ][ ][][] [ paper | enemyChoosing]
[ Action player] [scissors choosebox ] [rock] [paper] [ chosen | enemyChosen] -> [player ][ ][][] [ scissors | enemyChoosing]

(Vastustaja valitsee oman vaihtoehtonsa)
[wait1] -> [random options] again
[enemyChoosing] -> [wait1]

(Kumpi voittaa?)
up [rock | rock] [gameState] -> [rock | rock] [drawState]
up [rock | paper] [gameState] -> [rock | paper] [loseState]
up [rock | scissors] [gameState] -> [rock | scissors] [winState]
up [paper | rock] [gameState]-> [paper | rock] [winState]
up [paper | paper] [gameState]-> [paper | paper] [drawState]
up [paper | scissors] [gameState]-> [paper | scissors] [loseState]
up [scissors | rock] [gameState]-> [scissors | rock] [loseState]
up [scissors | paper] [gameState] -> [scissors | paper] [winState]
up [scissors | scissors] [gameState] -> [scissors | scissors] [drawState]

(Peli on päättynyt, aloitetaan timer viestin näyttämiseksi)
[ gameEndTimer2 ] -> [ gameHasEnded ] again
[ gameEndTimer1 ] -> [ gameEndTimer2 ] again
[ gameEndTimer0 ] -> [ gameEndTimer1 ] again
right [endState][gameEndTimer] -> [endstate][gameEndTimer0]

(Arvotaan päätösviesti lopputuloksen perusteella)
[drawState][gameHasEnded] -> [drawState][random drawMessage]
[winState][gameHasEnded] -> [moveToNextLevel][random winMessage]
[loseState][gameHasEnded] -> [loseState][random loseMessage]

[winMessage1] -> win message voitit
[winMessage2] -> win message noni voitit
[drawMessage1] -> restart message pelataa uusiks
[drawMessage2] -> restart message juuh tasapeli
[loseMessage1] -> restart message oho pelasin liian aikasi otetaa uusiks
[loseMessage2] -> restart message sori pelasin vääräs järjestykses otetaa uusiks


==============
WINCONDITIONS
==============
Some moveToNextLevel

=======
LEVELS
=======


(Fuksitaistelu)
gn..;:-.
..._!"@.
...£¤$%.
..e.€&/.
.pc.{¨~.
½AB*B§½B

(Teekkaritaistelu)
gn..ƒ„….
...†‡ˆ‰.
...Š‹ŒŽ.
..e‘€&/.
.pc’{¨~.
½AB*B§½B

(Vanhustaistelu)
gn..ƒ„….
...†“”‰.
...Š‹ŒŽ.
..e•€&/.
.pc–{¨~.
½AB*B§½B